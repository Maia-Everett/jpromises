apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

sourceCompatibility = 1.7
group = 'org.lucidfox.jpromises'
version = '0.1'

task wrapper(type: Wrapper) {
	gradleVersion = '2.12'
}

sourceSets {
	main {
		resources {
			exclude '**/.keep'
		}
	}

	gwt {
		java {
			srcDir 'src/main/java'
		}
		
		resources {
			srcDir 'src/main/resources'
			srcDir 'src/main/gwt'
			exclude '**/.keep'
		}
	}
}

jar {
	manifest {
		attributes 'Implementation-Title': 'JPromises', 'Implementation-Version': version
	}
}

repositories {
	mavenCentral()
}

dependencies {
	compileOnly group: 'com.google.gwt', name: 'gwt-user', version: '2.7.0'
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

test {
	testLogging {
		events 'passed', 'skipped', 'failed'
	}
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

task gwtJar(type: Jar) {
	baseName = "${project.name}-gwt"
	from sourceSets.gwt.allSource
}

task gwtJavadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	baseName = "${project.name}-gwt"
	classifier = 'javadoc'
	from javadoc.destinationDir
}

task gwtSourcesJar(type: Jar) {
	classifier = 'sources'
	baseName = "${project.name}-gwt"
	from sourceSets.gwt.allSource
}

artifacts {
	archives sourcesJar
	archives javadocJar
	archives gwtJar
	archives gwtJavadocJar
	archives gwtSourcesJar
}

uploadArchives {
	repositories {
	   mavenDeployer {
			repository(url: 'file:///home/maia/workspace/promises/jpromises/build/repo/')
			
			addFilter('default') {artifact, file ->
                artifact.name == "${project.name}"
            }
			
			addFilter('gwt') {artifact, file ->
                artifact.name == "${project.name}-gwt"
            }
		   
			pom('default').project {
				licenses {
					license {
						name 'MIT License'
						url 'http://www.opensource.org/licenses/mit-license.php'
						distribution 'repo'
					}
				}
			}
			
			pom('gwt').model.licenses = pom('default').model.licenses

        	pom('gwt').withXml {
        		// ugh, ugly
            	def dependency = asNode().get('dependencies')[0].appendNode('dependency')
            	dependency.appendNode('groupId', 'com.google.gwt')
      			dependency.appendNode('artifactId', 'gwt-user')
      			dependency.appendNode('version', '2.7.0')
      			dependency.appendNode('scope', 'provided')
      			
      			dependency = asNode().get('dependencies')[0].appendNode('dependency')
            	dependency.appendNode('groupId', "${project.group}")
      			dependency.appendNode('artifactId', "${project.name}")
      			dependency.appendNode('version', "${project.version}")
      			dependency.appendNode('scope', 'compile')
        	}
		}
	}
}
